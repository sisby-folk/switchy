plugins {
	id 'maven-publish'
	id 'com.github.breadmoirai.github-release'
	id 'com.modrinth.minotaur'
}

githubRelease {
	token = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN") ?: ""
	owner "sisby-folk"
	repo "switchy"
	tagName "v$baseVersion"
	releaseName "v$baseVersion"
	targetCommitish project.default_release_branch
	draft false
	prerelease false
	releaseAssets remapJar.archiveFile
	allowUploadToExisting true
	generateReleaseNotes true
}

tasks.githubRelease {
	dependsOn remapJar
}

modrinth {
	token = project.findProperty("modrinth.token") ?: System.getenv("MODRINTH_TOKEN")
	projectId = 'switchy'
	versionName = project.baseVersion
	versionNumber = "$project.baseVersion+$project.branch"
	versionType = 'release'
	uploadFile = remapJar
	gameVersions = [libs.versions.mc.get()]
	loaders = ['quilt', 'fabric']
	detectLoaders = false
	dependencies {
		optional.project "owo-lib"
		optional.project "drogtor"
		optional.project "fabrictailor"
		optional.project "pehkui"
		optional.project "origins"
		optional.project "fabrication"
		optional.project "styled-nicknames"
		optional.project "arcpocalypse"
		optional.project "lanyard"
		optional.project "figura"
	}
	changelog = "Changelog: https://github.com/sisby-folk/switchy/releases/tag/v$baseVersion"
	syncBodyFrom = rootProject.file("README.md").text
}

tasks.register("fullRelease") {
	group "publishing"
	dependsOn "githubRelease"
	dependsOn "modrinth"
	dependsOn "publish"
}
