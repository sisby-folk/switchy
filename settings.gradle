pluginManagement {
	repositories {
		maven {
			name = 'Quilt'
			url = 'https://maven.quiltmc.org/repository/release'
		}
		// Currently needed for Intermediary and other temporary dependencies
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		gradlePluginPortal()
	}
}

dependencyResolutionManagement {
	versionCatalogs {
		libs {
			def mc_major = '1.19'
			def mc = mc_major + '.2'

			// // Build Tools // //
			version('loom', '1.2.+')

			plugin('loom', 'org.quiltmc.loom').versionRef('loom')

			// // Base // //
			version('mc', mc)
			version('ql', "0.17.10")
			version('qm', "${mc}+build.22")
			version('qsl', "3.0.0-beta.29+${mc}")
			version('qfapi', "4.0.0-beta.28+0.73.2-${mc}")
			version('serverTranslationsApi', "1.4.18+${mc}")

			library('mc', 'mojang', 'minecraft').versionRef('mc')
			library('ql', 'org.quiltmc', 'quilt-loader').versionRef('ql')
			library('qm', 'org.quiltmc', 'quilt-mappings').versionRef('qm')
			library('qsl', 'org.quiltmc', 'qsl').versionRef('qsl')
			library('qfapi', 'org.quiltmc.quilted-fabric-api', 'quilted-fabric-api',).versionRef('qfapi')
			library('serverTranslationsApi', 'fr.catcore', 'server-translations-api').versionRef('serverTranslationsApi')

			// // Client // //

			// // UI // //
			version('owo', "0.9.3+${mc_major}")

			library('owo', 'io.wispforest', 'owo-lib').versionRef('owo')

			// // Cardinal // //
			version('cardinal', "5.1.0")

			library('cardinal', 'dev.onyxstudios.cardinal-components-api', 'cardinal-components-base').versionRef('cardinal')
			library('cardinalEntity', 'dev.onyxstudios.cardinal-components-api', 'cardinal-components-entity').versionRef('cardinal')

			// // Compat // //
			version('drogtor', "1.1.3${mc_major == '1.18' ? '' : "+${mc_major}"}")
			version('fabrictailor', "2.0.1")
			version('origins', "v1.7.1")
			version('apoli', "v2.6.0")
			version('calio', "1.9.0")
			version('pehkui', "3.4.2")
			version('fabrication', "3.0.2+${mc_major}")
			version('placeholderApi', "2.0.0-pre.1+${mc}")
			version('styledNicknames', "1.1.2+${mc_major}")
			version('placeholder', "2.0.0-beta.7+${mc_major}")
			version('playerData', "0.2.2+${mc}")
			version('luaj', "3.0.5-figura")
			version('figura', "0.1.0")

			library('drogtor', 'maven.modrinth', 'drogtor').versionRef('drogtor')
			library('fabrictailor', 'maven.modrinth', 'fabrictailor').versionRef('fabrictailor')
			library('origins', 'com.github.apace100', 'origins-fabric').versionRef('origins')
			library('apoli', 'com.github.apace100', 'apoli').versionRef('apoli')
			library('calio', 'com.github.apace100', 'calio').versionRef('calio')
			library('pehkui', 'com.github.Virtuoel', 'Pehkui').versionRef('pehkui')
			library('fabrication', 'maven.modrinth', 'fabrication').versionRef('fabrication')
			library('placeholderApi', 'eu.pb4', 'placeholder-api').versionRef('placeholderApi')
			library('styledNicknames', 'maven.modrinth', 'styled-nicknames').versionRef('styledNicknames')
			library('placeholder', 'eu.pb4', 'placeholder-api').versionRef('placeholder')
			library('playerData', 'eu.pb4', 'player-data-api').versionRef('playerData')
			library('luaj', 'com.github.Kingdom-of-The-Moon.luaj', 'luaj-core').versionRef('luaj')
			library('figura', 'com.github.Kingdom-of-The-Moon', 'FiguraRewriteRewrite').versionRef('figura')

			// // Local Debugging Convenience // //
			version('lazydfu', "0.1.3")
			version('fastload', "2.6.11")
			version('smoothboot-fabric', "1.19-1.7.1")
			version('sodium', "mc1.19.2-0.4.4")
			version('modmenu', "4.2.0-beta.2")

			library('lazydfu', 'maven.modrinth', 'lazydfu').versionRef('lazydfu')
			library('fastload', 'maven.modrinth', 'fastload').versionRef('fastload')
			library('smoothboot-fabric', 'maven.modrinth', 'smoothboot-fabric').versionRef('smoothboot-fabric')
			library('sodium', 'com.github.CaffeineMC', 'sodium-fabric').versionRef('sodium')
			library('modmenu', 'com.terraformersmc', 'modmenu').versionRef('modmenu')

			bundle('local_conveniences', [
					'lazydfu',
					'fastload',
					'smoothboot-fabric',
					'sodium',
					'modmenu'
			])
		}
	}
}

include('core', 'client', 'ui', 'cardinal', 'compat', 'compat-ui')
